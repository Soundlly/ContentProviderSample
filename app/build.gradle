apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "io.bitsound.contentprovidersample"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "application"
    productFlavors {

        def applicationNames = ["C.Provider Sample A", "C.Provider Sample B"]
        def applicationTitles = ["ContentProvider Sample A", "ContentProvider Sample B"]
        def applicationIds = ["io.bitsound.contentprovidersample.alpha", "io.bitsound.contentprovidersample.beta"]
        def providerAuthorities = applicationIds.collect { "${it}.provider" }

        alpha {
            dimension "application"
            def (local, shared) = [0, 1]

            applicationId applicationIds[local]
            def applicationName = applicationNames[local]
            def applicationTitle = applicationTitles[local]
            def localProviderAuthority = providerAuthorities[local]
            def sharedProviderAuthority = providerAuthorities[shared]
            def commonProviderPermission = "io.bitsound.contentprovidersample.provider.READWRITE"

            buildConfigField "String", "LOCAL_PROVIDER_AUTHORITY", "\"${localProviderAuthority}\""
            buildConfigField "String", "SHARED_PROVIDER_AUTHORITY", "\"${sharedProviderAuthority}\""
            manifestPlaceholders = [
                localProviderAuthority: localProviderAuthority,
                sharedProviderAuthority: sharedProviderAuthority,
                commonProviderPermission: commonProviderPermission,
            ]
            resValue "string", "app_name", applicationName
            resValue "string", "app_title", applicationTitle
            resValue "string", "localProviderAuthority", localProviderAuthority
            resValue "string", "sharedProviderAuthority", sharedProviderAuthority
            resValue "string", "commonProviderPermission", commonProviderPermission
        }
        beta {
            dimension "application"
            def (local, shared) = [1, 0]

            applicationId applicationIds[local]
            def applicationName = applicationNames[local]
            def applicationTitle = applicationTitles[local]
            def localProviderAuthority = providerAuthorities[local]
            def sharedProviderAuthority = providerAuthorities[shared]
            def commonProviderPermission = "io.bitsound.contentprovidersample.provider.READWRITE"

            buildConfigField "String", "LOCAL_PROVIDER_AUTHORITY", "\"${localProviderAuthority}\""
            buildConfigField "String", "SHARED_PROVIDER_AUTHORITY", "\"${sharedProviderAuthority}\""
            manifestPlaceholders = [
                localProviderAuthority: localProviderAuthority,
                sharedProviderAuthority: sharedProviderAuthority,
                commonProviderPermission: commonProviderPermission,
            ]
            resValue "string", "app_name", applicationName
            resValue "string", "app_title", applicationTitle
            resValue "string", "localProviderAuthority", localProviderAuthority
            resValue "string", "sharedProviderAuthority", sharedProviderAuthority
            resValue "string", "commonProviderPermission", commonProviderPermission
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    /* Kotlin */
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    /* Android Support Library */
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    /* Unit Test */
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.9.0'
    testImplementation 'org.robolectric:robolectric:3.7.1'

    /* Android Test */
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
